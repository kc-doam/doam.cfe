Процедура ДобавитьЗапись(Ссылка, МобильноеПриложение, Отметка) Экспорт
	
	НЗ = РегистрыСведений.МП_ОтправленныеСсылочныеДанные.СоздатьНаборЗаписей();
	НЗ.ОбменДанными.Загрузка = Истина;
	НЗ.Отбор.МобильноеПриложение.Установить(МобильноеПриложение);
	НЗ.Отбор.Ссылка.Установить(Ссылка);
	
	МЗ = НЗ.Добавить();
	МЗ.МобильноеПриложение = МобильноеПриложение;
	МЗ.Ссылка = Ссылка;
	МЗ.ОтметкаВремени = Отметка;
	
	НЗ.Записать();
	
КонецПроцедуры

Функция ОтметкаВремениУстарела(МобильноеПриложение, Ссылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОтметкиВремениСсылочныхДанных.Отметка КАК Отметка
		|ИЗ
		|	РегистрСведений.МП_ОтправленныеСсылочныеДанные КАК МП_ОтправленныеСсылочныеДанные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхДанных КАК ОтметкиВремениСсылочныхДанных
		|		ПО МП_ОтправленныеСсылочныеДанные.Ссылка = ОтметкиВремениСсылочныхДанных.Ссылка
		|			И МП_ОтправленныеСсылочныеДанные.ОтметкаВремени < ОтметкиВремениСсылочныхДанных.Отметка
		|ГДЕ
		|	МП_ОтправленныеСсылочныеДанные.Ссылка = &Ссылка
		|	И МП_ОтправленныеСсылочныеДанные.МобильноеПриложение = &МобильноеПриложение";
	
	Запрос.УстановитьПараметр("МобильноеПриложение", МобильноеПриложение);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура ДобавитьНаборЗаписейИзТаблицы(Таблица, МобильноеПриложение, 
		ОтметкаВремениПоУмолчанию) Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	ЕстьПометкаУдаления = (Таблица.Колонки.Найти("ПометкаУдаления") <> Неопределено);
	
	ЕстьОтметкаВремени = (Таблица.Колонки.Найти("ОтметкаВремени") <> Неопределено);
	
	Для каждого Строка Из Таблица Цикл
		
		Если ЕстьПометкаУдаления И Строка.ПометкаУдаления = Истина Тогда
			ОчиститьОтправленныеДанные(МобильноеПриложение, Строка.Ссылка);
		Иначе
			
			Если Не ЕстьОтметкаВремени Или Не ЗначениеЗаполнено(Строка.ОтметкаВремени) Тогда
				ОтметкаВремени = ОтметкаВремениПоУмолчанию;
			Иначе
				ОтметкаВремени = Строка.ОтметкаВремени;
			КонецЕсли;
			
			ДобавитьЗапись(Строка.Ссылка, МобильноеПриложение, ОтметкаВремени);
	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьОтправленныеДанные(МобильноеПриложение, Ссылка) Экспорт
	
	НЗ = СоздатьНаборЗаписей();
	НЗ.ОбменДанными.Загрузка = Истина;
	НЗ.Отбор.МобильноеПриложение.Установить(МобильноеПриложение);
	НЗ.Отбор.Ссылка.Установить(Ссылка);
	НЗ.Записать();
	
КонецПроцедуры