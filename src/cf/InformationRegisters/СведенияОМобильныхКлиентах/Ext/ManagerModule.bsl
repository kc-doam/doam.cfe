// Записывает сведения о мобильном клиенте.
// Параметры:
//	МобильныйКлиент - ссылка на узел плана обмена Мобильный
//	Дата - дата и время последнего подключения
//	Сведения - строка с описанием мобильного клиента
Процедура ЗаписатьСведенияОКлиенте(МобильноеПриложение, Дата, Описание, ВерсияКлиента) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.СведенияОМобильныхКлиентах.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.МобильныйКлиент = МобильноеПриложение;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.МобильныйКлиент = МобильноеПриложение;
	МенеджерЗаписи.ДатаПоследнейАктивности = Дата;
	Если Не ЗначениеЗаполнено(МенеджерЗаписи.Описание) Или ЗначениеЗаполнено(Описание) Тогда
		МенеджерЗаписи.Описание = Описание;
	КонецЕсли;
	МенеджерЗаписи.ВерсияКлиента = ВерсияКлиента;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры
	
// Возвращает дату последнего подключения мобильного клиента
// Параметры:
//	МобильныйКлиент - ссылка на узел плана обмена Мобильный
Функция ПолучитьДатуПоследнейАктивностиКлиента(МобильноеПриложение) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОМобильныхКлиентах.ДатаПоследнейАктивности
		|ИЗ
		|	РегистрСведений.СведенияОМобильныхКлиентах КАК СведенияОМобильныхКлиентах
		|ГДЕ
		|	СведенияОМобильныхКлиентах.МобильныйКлиент = &МобильноеПриложение";
	Запрос.УстановитьПараметр("МобильноеПриложение", МобильноеПриложение);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ДатаПоследнейАктивности;
	Иначе
		Возврат Дата(1,1,1);
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСведения(МобильноеПриложение) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОМобильныхКлиентах.ДатаПоследнейАктивности КАК ДатаПоследнейАктивности,
		|	СведенияОМобильныхКлиентах.Описание КАК Описание,
		|	СведенияОМобильныхКлиентах.ВерсияКлиента КАК ВерсияКлиента,
		|	СведенияОМобильныхКлиентах.ОтметкаВремениСборкиДанных КАК ОтметкаВремениСборкиДанных,
		|	СведенияОМобильныхКлиентах.ДатаПервоначальнойЗагрузкиДанных КАК ДатаПервоначальнойЗагрузкиДанных
		|ИЗ
		|	РегистрСведений.СведенияОМобильныхКлиентах КАК СведенияОМобильныхКлиентах
		|ГДЕ
		|	СведенияОМобильныхКлиентах.МобильныйКлиент = &МобильноеПриложение";
	Запрос.УстановитьПараметр("МобильноеПриложение", МобильноеПриложение);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
	
КонецФункции

Функция ПолучитьВерсию(МобильноеПриложение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Сведения = ПолучитьСведения(МобильноеПриложение);
	
	Если Сведения <> Неопределено Тогда
		Возврат Сведения.ВерсияКлиента;
	КонецЕсли; 
	
	Возврат "";
	
КонецФункции

Функция ПолучитьДатуПервоначальнойЗагрузкиДанных(МобильноеПриложение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Сведения = ПолучитьСведения(МобильноеПриложение);
	
	Если Сведения <> Неопределено Тогда
		Возврат Сведения.ДатаПервоначальнойЗагрузкиДанных;
	КонецЕсли; 
	
КонецФункции

Процедура ЗаписатьДатуПервоначальнойЗагрузкиДанных(МобильноеПриложение, Дата) Экспорт
	
	МЗ = СоздатьМенеджерЗаписи();
	МЗ.МобильныйКлиент = МобильноеПриложение;
	МЗ.Прочитать();
	МЗ.ДатаПервоначальнойЗагрузкиДанных = Дата;
	МЗ.Записать();
	
КонецПроцедуры

Процедура ЗаписатьОтметкуСборкиДанных(МобильноеПриложение, Отметка) Экспорт
	
	МЗ = РегистрыСведений.СведенияОМобильныхКлиентах.СоздатьМенеджерЗаписи();
	МЗ.МобильныйКлиент = МобильноеПриложение;
	МЗ.Прочитать();
	МЗ.ОтметкаВремениСборкиДанных = Отметка;
	МЗ.Записать();
	
КонецПроцедуры

Функция ПолучитьОтметкуВремени(МобильноеПриложение) Экспорт
	
	Сведения = ПолучитьСведения(МобильноеПриложение);
	
	Если Сведения <> Неопределено Тогда
		Возврат Сведения.ОтметкаВремениСборкиДанных;	
	КонецЕсли; 
	
КонецФункции
